/*
 * Pseudocódigo, sintaxe utilizada em Ascencio (2012).
 * 
 * Descrição:
 * 
 *   Define um novo tipo de dados e sub-rotinas para manipulá-lo.
 *
 * Referência:
 *
 *   ASCENCIO, Ana Fernanda Gomes; CAMPOS, Edilene Aparecida Veneruchi De. 
 *   Fundamentos da Programação de Computadores: Algoritmos, Pascal, C/c++ 
 *   (Padrão Ansi) e Java. 3. ed. São Paulo: Pearson, 2012.
 * 
 * Autor:
 *
 *   Prof. Me. Leonardo Villani
 *
 */

ALGORITMO
  DECLARE contas[5] REGISTRO(numero NUMÉRICO, cliente TEXTO, saldo NUMÉRICO)
  DECLARE opcao NUMÉRICO
  FAÇA INÍCIO
    ESCREVA("Escolha uma opção:")
    ESCREVA("1. Cadastrar contas")
    ESCREVA("2. Visualizar contas")
    ESCREVA("3. Excluir conta")
    ESCREVA("4. Sair")
    ESCREVA("Opção: ")
    LEIA(opcao)
    ESCOLHA (opcao) INÍCIO
      CASO 1:
        cadastrarContas(contas)
        PARE
      CASO 2:
        visualizarContas(contas)
        PARE
      CASO 3:
        excluirConta(contas)
        PARE
      CASO 4:
        ESCREVA("Programa finalizado!")
        PARE
      CASO PADRAO:
        ESCREVA("Opção inválida!")
    FIM_ESCOLHA
  FIM ENQUANTO (opcao != 4)
FIM_ALGORITMO

/*
 * Define os valores dos atributos de um novo tipo de dado, armazenado em um 
 * elemento de um vetor.
 *
 */
SUB-ROTINA cadastrarContas(contas[5] REGISTRO(numero NUMÉRICO, cliente TEXTO, saldo NUMÉRICO)) INÍCIO

  DECLARE i NUMÉRICO
  PARA i <- 0 ATÉ 5 FAÇA INÍCIO
    ESCREVA("Informe o número da conta: ")
    LEIA(contas[i].numero)
    ESCREVA("Informe o nome do cliente: ")
    LEIA(contas[i].cliente)
    ESCREVA("Informe o saldo da conta: ")
    LEIA(contas[i].saldo)
  FIM_PARA

FIM_SUB-ROTINA

/*
 * Exibe os valores dos atributos de um novo tipo de dado, armazenado em um 
 * elemento de um vetor e que corresponde ao filtro definido pelo usuário.
 *
 */
SUB-ROTINA visualizarContas(contas[5] REGISTRO(numero NUMÉRICO, cliente TEXTO, saldo NUMÉRICO)) INÍCIO

  DECLARE cliente TEXTO
  DECLARE i NUMÉRICO
  ESCREVA("Informe o nome do cliente:")
  LEIA(cliente)
  PARA i <- 0 ATÉ 5 FAÇA INÍCIO
    SE contas[i].cliente = cliente ENTÃO INÍCIO
      ESCREVA("Número: ", contas[i].numero)
      ESCREVA("Saldo: ", contas[i].saldo)
    FIM_SE
  FIM_PARA

FIM_SUB-ROTINA

/*
 * Zera os valores dos atributos de um novo tipo de dado, armazenado em um 
 * elemento de um vetor e que corresponde ao filtro definido pelo usuário.
 *
 */
SUB-ROTINA excluirConta(contas[5] REGISTRO(numero NUMÉRICO, cliente TEXTO, saldo NUMÉRICO)) INÍCIO

  DECLARE i, menor NUMÉRICO
  menor <- 0

  // identificando conta com menor saldo
  PARA i <- 0 ATÉ 5 FAÇA INÍCIO
    SE i = 0 OU contas[i].saldo < contas[menor].saldo ENTÃO INÍCIO
      menor <- i
    FIM_SE
  FIM_PARA 

  // excluindo conta com o menor saldo encontrado
  contas[menor].numero = 0
  contas[menor].nome = ""
  contas[menor].saldo = 0

FIM_SUB-ROTINA
